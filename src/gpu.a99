       xorg gpu_program

       li   r15,>47fe                  ; Stack pointer
       li   r0,>0100                   ; dx (FP 8.8)
       li   r1,>0000                   ; dy (FP 8.8)
gpu_loop:
       call @draw_bitmap
       dec  r0
;       idle
       jmp  gpu_loop

*********************************************************************************
*
* Draw bitmap
*
* Source address is 000YYYYY.YYXXXXXX
*
draw_bitmap:
       clr  r2                         ; x (FP 8.8, XXXXXXXX.xxxxxxxx)
       clr  r3                         ; y (FP 8.8, YYYYYYYY.yyyyyyyy)
       li   r11,>0100                  ; x bit determining left or right pixel
       li   r4,bitmap_base
       li   r5,bitmap_height
       clr  r10
draw_bitmap_1:
       li   r6,bitmap_width/2
       mov  r2,r7                      ; Save x
       mov  r3,r8                      ; Save y
draw_bitmap_2:
*      Left destination pixel
       mov  r2,r9                      ; XXXXXXXX.xxxxxxxx
       srl  r9,7                       ; 00000000.XXXXXXXx
       movb r3,r9                      ; YYYYYYYY.XXXXXXXx
       sla  r9,1                       ; YYYYYYYX.XXXXXXx0
       srl  r9,3                       ; 000YYYYY.YYXXXXXX
       movb @bitmap_base_2(r9),r10
*      Isolate pixel in left nybble
       coc  r11,r2                     ; Left or right pixel?
       jne  draw_bitmap_3
       sla  r10,4                      ; Move right pixel into position
       jmp  draw_bitmap_3a
draw_bitmap_3:
       andi r10,>f000
draw_bitmap_3a:
*      Move x and y to next pixel
       a    r0,r2                      ; x += dx
       a    r1,r3                      ; y += dy
*      Right destination pixel
       mov  r2,r9                      ; XXXXXXXX.xxxxxxxx
       srl  r9,7                       ; 00000000.XXXXXXXx
       movb r3,r9                      ; YYYYYYYY.XXXXXXXx
       sla  r9,1                       ; YYYYYYYX.XXXXXXx0
       srl  r9,3                       ; 000YYYYY.YYXXXXXX
       movb @bitmap_base_2(r9),r12
*      Isolate pixel in right nybble
       coc  r11,r2                     ; Left or right pixel?
       jeq  draw_bitmap_4
       srl  r12,4                      ; Move left pixel into position
       jmp  draw_bitmap_4a
draw_bitmap_4:
       andi r12,>0f00
draw_bitmap_4a:
*      Move x and y to next pixel
       a    r0,r2                      ; x += dx
       a    r1,r3                      ; y += dy
*      Write to destination
       socb r12,r10                    ; Combine left and right pixels
       movb r10,*r4+                   ; Write byte to destination
*      Next column
       dec  r6
       jne  draw_bitmap_2
*      Next row
       mov  r7,r2                      ; Restore x
       mov  r8,r3                      ; Restore y
       s    r1,r2                      ; x -= dy
       a    r0,r3                      ; y += dx
       dec  r5
       jne  draw_bitmap_1
       ret
*// draw_bitmap

       aorg
