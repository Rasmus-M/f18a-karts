       xorg gpu_program

       li   r15,>47fe                  ; Stack pointer
gpu_loop:
       idle
       mov  @dx,r0
       mov  @dy,r1
       mov  @x,r2
       mov  @y,r3

       call @draw_bitmap
       jmp  gpu_loop

x0:                                    ; Center x
       data >4000
y0:                                    ; Center y
       data >4000
x:
       data >4000
y:
       data >4000
dx:
       data >0000
dy:
       data >0000

*********************************************************************************
*
* Draw bitmap
*
* r0: dx (FP 8.8)
* r1: dy (FP 8.8)
* r2: x (FP 8.8, XXXXXXXX.xxxxxxxx)
* r3: y (FP 8.8, YYYYYYYY.yyyyyyyy)
* Source address is 000YYYYY.YYXXXXXX
*
draw_bitmap:
*      Adjust
       mov  r0,r13                     ; dx
       sla  r13,5                      ; 32 * dx
       s    r13,r3                     ; y -= 32 * dx
       sla  r13,1                      ; 64 * dx
       s    r13,r2                     ; x -= 64 * dx
       mov  r1,r13                     ; dy
       sla  r13,5                      ; 32 * dy
       a    r13,r2                     ; x += 32 * dy
       sla  r13,1                      ; 64 * dy
       s    r13,r3                     ; y -= 64 * dy
*      Init
       li   r11,>0100                  ; x bit determining left or right pixel
       li   r4,bitmap_base
       li   r5,bitmap_height_1
       clr  r10
draw_bitmap_1:
       li   r6,bitmap_width/2
       mov  r2,r7                      ; Save x
       mov  r3,r8                      ; Save y
draw_bitmap_2:
*      Left destination pixel
       mov  r2,r9                      ; XXXXXXXX.xxxxxxxx
       srl  r9,7                       ; 0000000X.XXXXXXXx
       movb r3,r9                      ; YYYYYYYY.XXXXXXXx
       sla  r9,1                       ; YYYYYYYX.XXXXXXx0
       srl  r9,3                       ; 000YYYYY.YYXXXXXX
       movb @bitmap_base_2(r9),r10
*      Isolate pixel in left nybble
       coc  r11,r2                     ; Left or right pixel?
       jne  draw_bitmap_3
       sla  r10,4                      ; Move right pixel into position
       jmp  draw_bitmap_3a
draw_bitmap_3:
       andi r10,>f000
draw_bitmap_3a:
*      Move x and y to next pixel
       a    r0,r2                      ; x += dx
       a    r1,r3                      ; y += dy
*      Right destination pixel
       mov  r2,r9                      ; XXXXXXXX.xxxxxxxx
       srl  r9,7                       ; 0000000X.XXXXXXXx
       movb r3,r9                      ; YYYYYYYY.XXXXXXXx
       sla  r9,1                       ; YYYYYYYX.XXXXXXx0
       srl  r9,3                       ; 000YYYYY.YYXXXXXX
       movb @bitmap_base_2(r9),r12
*      Isolate pixel in right nybble
       coc  r11,r2                     ; Left or right pixel?
       jeq  draw_bitmap_4
       srl  r12,4                      ; Move left pixel into position
       jmp  draw_bitmap_4a
draw_bitmap_4:
       andi r12,>0f00
draw_bitmap_4a:
*      Move x and y to next pixel
       a    r0,r2                      ; x += dx
       a    r1,r3                      ; y += dy
*      Write to destination
       socb r12,r10                    ; Combine left and right pixels
       movb r10,*r4+                   ; Write byte to destination
*      Next column
       dec  r6
       jne  draw_bitmap_2
*      Next row
       mov  r7,r2                      ; Restore x
       mov  r8,r3                      ; Restore y
       s    r1,r2                      ; x -= dy
       a    r0,r3                      ; y += dx
       dec  r5
       jne  draw_bitmap_1
       ret
*// draw_bitmap

       aorg
