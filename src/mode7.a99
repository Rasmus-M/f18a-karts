*********************************************************************
*
* Mode 7 demo
* For the TI-99/4A home computer
*
* January 2024 -
* Rasmus Moustgaard <rasmus.moustgaard@gmail.com>
*

       copy "macros.a99"

**
* Constants
*
screen_width_char:
       equ  32
screen_height_char:
       equ  30
screen_size_char:
       equ  screen_width_char*screen_height_char
screen_width:
       equ  screen_width_char*8
screen_height:
       equ  screen_height_char*8
background_image_height_char:
       equ  6
bitmap_top:
       equ  background_image_height_char*8
bitmap_width:
       equ  128
bitmap_dst_height:
       equ  63
bitmap_gap_top:
       equ  bitmap_top+bitmap_dst_height
bitmap_gap_height:
       equ  1
bitmap_src_top:
       equ  bitmap_gap_top+bitmap_gap_height
bitmap_src_height:
       equ  128
bitmap_height:
       equ  bitmap_dst_height+bitmap_gap_height+bitmap_src_height
n_char_patterns:
       equ  16
n_sprite_patterns:
       equ  2
max_sprite_dist:
       equ  >4000
min_sprite_dist:
       equ  >0200
n_other_carts:
       equ  5
**
* Car physics
*
max_power_road:
       equ  >00a0
max_power_terrain:
       equ  >0018
max_reverse_road:
       equ  >0050
max_reverse_terrain:
       equ  >000d
power_factor:
       equ  >0002
reverse_factor:
       equ  >0001
drag:
       equ  >00f4
turn_speed:
       equ  >0010
angular_drag:
       equ  >00f4

**
* Memory mapped addresses
*
vdpwd:                                 ; VDP write data
       equ  >8c00
vdpwa:                                 ; VDP set read/write address
       equ  >8c02
vdprd:                                 ; VDP read data
       equ  >8800
vdpsta:                                ; VDP status
       equ  >8802
sound:                                 ; Sound
       equ  >8400
spchwt:
       equ  >9400
spchrd:
       equ  >9000


**
* VDP memory map
*
gpu_program_tmp:
       equ  >0000
bitmap_base:                           ; 128 x 192 x 4 bbp = 12K
       equ  >0000
bitmap_dst:
       equ  bitmap_base
bitmap_gap:
       equ  bitmap_base + (bitmap_dst_height * bitmap_width / 2)
bitmap_src:
       equ  bitmap_gap + (bitmap_gap_height * bitmap_width / 2)
name_table_2:                          ; Name table 2
       equ  bitmap_src + (bitmap_src_height * bitmap_width / 2) ; = >3000
gpu_data:                              ; Data shared between CPU and GPU (64 bytes)
       equ  >33c0
name_table:                            ; Name table 1 (note: we only need 6 rows of this)
       equ  >3400
tile_attr_table:                       ; Tile attribute table
       equ  >3500
sprite_attr_table:                     ; Sprite attribute table
       equ  >3580
graphics_data_1:                       ; >200 bytes for scaled sprites
       equ  >3600
pattern_table:                         ; Pattern table
       equ  >3800
sprite_pattern_table:                  ; Sprite pattern table
       equ  >3800
graphics_data_2:                       ; >300 bytes for scaled sprites
       equ  >3900
pattern_table_p1:                      ; Pattern table plane 1
       equ  >3c00
sprite_pattern_table_p1:               ; Sprite pattern table plane 1
       equ  >3c00
graphics_data_3:                       ; >300 bytes for scaled sprites
       equ  >3d00
gpu_program:                           ; Approx. >550 bytes for scaled sprites
       equ  >4000

**
* Scratch pad
*
pad:
       equ  >8300
wrksp:
       equ  pad                        ; Workspace
r0lb:
       equ  wrksp+1
r1lb:
       equ  wrksp+3
r2lb:
       equ  wrksp+5
r3lb:
       equ  wrksp+7
wrksp2:
       equ  wrksp+>20
stack:
       equ  wrksp2+>20
**
* PAD constants
*
padcon:
       equ  stack+>16                  ; Start of pad constants
one:
       equ  padcon
**
* PAD variables
*
padvar:                                ; Start of pad variables
       equ  one+2
frame:
       equ padvar
x:                                     ; World x position (FP 8.8, unsigned, 0 <= x < 256)
       equ  frame+2
y:                                     ; World y position (FP 8.8, unsigned, 0 <= x < 128)
       equ  x+2
dx:                                    ; Direction vector x (FP 8.8, signed, -1 <= dx <= 1)
       equ  y+2
dy:                                    ; Direction vector y (FP 8.8, signed, -1 <= dy <= 1)
       equ  dx+2
vx:                                    ; Velocity vector x (FP 8.8, signed)
       equ  dy+2
vy:                                    ; Velocity vector y (FP 8.8, signed)
       equ  vx+2
angle:                                 ; View angle, 0 - 255, higher values rotates clockwise (FP 8.8)
       equ  vy+2
angular_velocity:                      ; Angular velocity
       equ  angle+2
power:                                 ;
       equ  angular_velocity+2
reverse:
       equ  power+2
max_power:                                 ;
       equ  reverse+2
max_reverse:
       equ  max_power+2
speed:
       equ  max_reverse+2
steering:                              ; User input, -1=left, 1=right
       equ  speed+2
throttle:                              ; User input, throttle pressed
       equ  steering+2
brake:                                 ; User input, brake/reverse pressed
       equ  throttle+2
vdp_copy_pad:
       equ  brake+2
pad_max:
       equ  vdp_copy_pad+22

********************************************************************************
*
* Main program
*
       aorg >2000
main:
       limi 0                          ; Interrupts off
       lwpi wrksp                      ; Setup workspace
       li   r10,stack                  ; Setup stack pointer
       li   r15,vdpwd
       bl   @one_time_init
*      Run GPU program, which moves it into high GPU RAM
       bl   @run_gpu
*      Init
       li   r0,init_data
       li   r1,x
       li   r2,init_data_end-init_data
       bl   @copy
       bl   @calculate_dx_dy
       bl   @upload_gpu_data
       bl   @trigger_gpu
loop:
       bl   @vsync
       bl   @display_sprites
       bl   @scroll_background
       bl   @user_input
       bl   @move
       bl   @collisions
       bl   @move_other_karts
       bl   @upload_gpu_data
       jmp  loop

*********************************************************************
*
* Wait for vertical retrace (CRU)
*
vsync:
       .proc
       movb @vdpsta,r12
vsync_1:
       clr  r12
       tb   2                          ; Test CRU bit for VDP interrupt
       jeq  vsync_1
       movb @vdpsta,r12
       .endproc
*// vsync

*********************************************************************************
*
* Run GPU program
*
run_gpu:
       .proc
       li   r0,gpu_program_tmp/256+>3600
       bl   @vwtr
       li   r0,gpu_program_tmp%256+>3700
       bl   @vwtr
       .endproc
*// run_gpu

*********************************************************************************
*
* Wait for the GPU to be idle
*
wait_gpu:
       .proc
       li   r0,>0f02
       bl   @vwtr
wait_gpu_1:
       movb @vdpsta,r0
       jlt  wait_gpu_1
       li   r0,>0f00
       bl   @vwtr
       .endproc
*// wait_gpu

*********************************************************************************
*
* Resume GPU after idle
*
trigger_gpu:
       .proc
       li   r0,>3801
       bl   @vwtr
       .endproc
*// wait_gpu

*********************************************************************************
*
* Display sprites
*
display_sprites:
       .proc
       li   r0,sprite_attr_table
       bl   @vwad
*      Kart left sprite
       li   r1,>4e60
       mov  r1,*r15
       swpb r1
       mov  r1,*r15
       li   r1,>1404
       mov  r1,*r15
       swpb r1
       mov  r1,*r15
*      Kart right sprite
       li   r1,>4e80
       mov  r1,*r15
       swpb r1
       mov  r1,*r15
       li   r1,>1444
       mov  r1,*r15
       swpb r1
       mov  r1,*r15
*      Marker on source map
       mov  @y,r1
       andi r1,>7f00
       ai   r1,->1000
       ai   r1,bitmap_src_top*256
       movb r1,*r15
       mov  @x,r1
       ai   r1,->1400
       movb r1,*r15
       li   r1,>1000
       movb r1,*r15
       swpb r1
       movb r1,*r15
       .endproc
*// display_sprites

*********************************************************************************
*
* Scroll background
*
scroll_background:
       .proc
       mov  @angle,r1
       srl  r1,8
       mov  r1,r0
       sla  r0,2
       andi r0,>00ff
       ori  r0,>1900
       bl   @vwtr
       mov  r1,r0
       sla  r0,1
       andi r0,>00ff
       ori  r0,>1b00
       bl   @vwtr
       .endproc
*// scroll_background

*********************************************************************************
*
* User input
*
user_input:
       .proc
       clr  @throttle
       clr  @brake
       clr  @steering
*      Fire
       li   r0,JOY_FI
       bl   @check_key
       jeq  user_input_1
       seto @brake
user_input_1:
*      Left
       tb   1
       jeq  user_input_2
       dec  @steering
       jmp  user_input_3
user_input_2:
*      Right
       tb   2
       jeq  user_input_3
       inc  @steering
user_input_3:
*      Backwards
       tb   3
       jeq  user_input_4
       seto @brake
user_input_4:
*      Forwards
       tb   4
       jeq  user_input_5
       seto @throttle
user_input_5:
       .endproc
*// user_input

*********************************************************************************
*
* Move
*
move:
       .proc
*      Throttle
       li   r0,power_factor
       mov  @power,r1
       abs  @throttle
       jeq  move_1
       a    r0,r1                      ; power += power_factor
       c    r1,@max_power
       jlt  move_2
       mov  @max_power,r1
       jmp  move_2
move_1:
       s    r0,r1                       ; power -= power_factor
       jgt  move_2
       clr  r1
move_2:
       mov  r1,@power
*      Brake
       li   r0,reverse_factor
       mov  @reverse,r1
       abs  @brake
       jeq  move_3
       a    r0,r1                      ; reverse += reverse_factor
       c    r1,@max_reverse
       jlt  move_4
       mov  @max_reverse,r1
       jmp  move_4
move_3:
       s    r0,r1                      ; reverse -= reverse_factor
       jgt  move_4
       clr  r1
move_4:
       mov  r1,@reverse
*      Can we turn?
       mov  @steering,r0
       jeq  move_9                     ; Not without steering
       mov  @power,r0
       jgt  move_5                     ; Yes with power
       mov  @reverse,r0
       jeq  move_9                     ; Not without reverse
*      Angular velocity
move_5:
       mov  @angular_velocity,r1
       c    @power,@reverse
       jlt  move_7
*      Angular velocity - forwards direction
       mov  @steering,r0
       jlt  move_6
       ai   r1,turn_speed              ; angular_velocity += turn_speed
       jmp  move_9
move_6:
       ai   r1,-turn_speed             ; angular_velocity -= turn_speed
       jmp  move_9
move_7:
*      Angular velocity - backwards direction
       mov  @steering,r0
       jlt  move_8
       ai   r1,-turn_speed             ; angular_velocity -= turn_speed
       jmp  move_9
move_8:
       ai   r1,turn_speed              ; angular_velocity += turn_speed
move_9:
       mov  r1,@angular_velocity
*      Speed
       mov  @power,@speed              ; speed = power - reverse
       s    @reverse,@speed
*      Directional vector
       bl   @calculate_dx_dy           ; dx = cos(angle), dy = sin(angle)
*      Velocity
       mov  @dx,r1                     ; vx = dx * speed
       mov  @speed,r2
       bl   @mpy_fp88
       mov  r1,@vx
       mov  @dy,r1                     ; vy = dy * speed
       mov  @speed,r2
       bl   @mpy_fp88
       mov  r1,@vy
*      Position
       a    @vx,@x                     ; x += vx
       a    @vy,@y                     ; y += vy
*      Drag
       mov  @vx,r1                     ; vx *= drag
       li   r2,drag
       bl   @mpy_fp88
       mov  r1,@vx
       mov  @vy,r1                     ; vy *= drag
       li   r2,drag
       bl   @mpy_fp88
       mov  r1,@vy
*      angle
       a    @angular_velocity,@angle   ; angle += angular_velocity
*      angular velocity
       mov  @angular_velocity,r1       ; angular_velocity *= angular_drag
       li   r2,angular_drag
       bl   @mpy_fp88
       mov  r1,@angular_velocity
       .endproc
*// move

*********************************************************************************
*
* Calculate directional vector
*
calculate_dx_dy:
       .proc
       mov  @angle,r0
       srl  r0,8
*      dx
       mov  r0,r1
       bl   @cos
       mov  r1,@dx
*      dy
       mov  r0,r1
       bl   @sin
       mov  r1,@dy
       .endproc
*// calculate_dx_dy

*********************************************************************************
*
* User collisions
*
collisions:
       .proc
       mov  @x,r0
       movb @y,r1
       bl   @get_map_pixel
       ci   r1,>0200
       jeq  collisions_1
       ci   r1,>0a00
       jne  collisions_2
collisions_1:
*      Collision
       li   r0,max_power_terrain
       mov  r0,@max_power
       li   r0,max_reverse_terrain
       mov  r0,@max_reverse
       jmp  collisions_3
collisions_2:
*      No collision
       li   r0,max_power_road
       mov  r0,@max_power
       li   r0,max_reverse_road
       mov  r0,@max_reverse
collisions_3:
       .endproc
*// collisions

*********************************************************************************
*
* Move other karts
*
move_other_karts:
       .proc
       li   r3,other_karts
       li   r5,n_other_carts
move_other_karts_1:
       mov  *r3+,r0
       mov  *r3+,r1
       bl   @get_dir_map_vector
       ai   r3,-4
       sra  r0,1
       a    r0,*r3+
       sra  r1,1
       a    r1,*r3+
       dec  r5
       jne  move_other_karts_1
       .endproc
*// move_other_karts

*********************************************************************************
*
* Upload GPU data
*
upload_gpu_data:
       .proc
*      x
       li   r0,gpu_x
       mov  @x,r1
       bl   @vsww
*      y
       li   r0,gpu_y
       mov  @y,r1
       bl   @vsww
*      dx
       li   r0,gpu_dx
       mov  @dx,r1
       bl   @vsww
*      dy
       li   r0,gpu_dy
       mov  @dy,r1
       bl   @vsww
*      Other karts
       li   r0,gpu_kart_sprites+2
       li   r1,other_karts
       li   r2,n_other_carts
upload_gpu_data_1:
       bl   @vwad
       movb *r1+,*r15                  ; x
       movb *r1+,*r15
       movb *r1+,*r15                  ; y
       movb *r1+,*r15
       ai   r0,gpu_sprite_size
       dec  r2
       jne  upload_gpu_data_1
       .endproc
*// upload_gpu_data

*********************************************************************************
*
* Get map pixel
*
* r0: x (FP 8.8)
* r1: y (FP 8.8)
*
* On return r1 MSB contains the pixel value (0-15)
*
get_map_pixel:
       movb r0,r2                      ; XXXXXXXX.xxxxxxxx
       swpb r2                         ; xxxxxxxx.XXXXXXXX
       movb r1,r2                      ; YYYYYYYY.XXXXXXXX
       sla  r2,1                       ; YYYYYYYX.XXXXXXX0
       srl  r2,3                       ; 000YYYYY.YYXXXXXX
       movb @bmp(r2),r1                ; Get 2 source pixels
       andi r0,>0200
       jne  get_map_pixel_1
       srl  r1,4
get_map_pixel_1:
       andi r1,>0f00
       rt
*// get_map_pixel

*********************************************************************************
*
* Get directional vector from the map
*
* r0: x (FP 8.8)
* r1: y (FP 8.8)
*
* On return r0 contains dx as FP 8.8
* On return r1 contains dy as FP 8.8
*
get_dir_map_vector:
       srl  r1,4                       ; 0000YYYY.YYYYyyyy
       andi r1,>07c0                   ; 00000YYY.YY000000
       srl  r0,10                      ; 00000000.00XXXXXX
       soc  r0,r1                      ; 00000YYY.YYXXXXXX
       clr  r2
       movb @dir_map(r1),r2
       srl  r2,2
       mov  @sin_table(r2),r1
       ai   r2,128
       andi r2,>01ff
       mov  @sin_table(r2),r0
       rt
*// get_dir_map_vector

*********************************************************************************
*
* One time initializations
*
one_time_init:
       .proc
       li   r0,1
       mov  r0,@one
       bl   @clear_pad
       bl   @copy_vdp_copy_to_pad
       bl   @graphics_mode
       bl   @init_vdp_ram
       .endproc
*// one_time_init

*********************************************************************************
*
* Clear PAD
*
clear_pad:
       li   r1,padvar
       li   r2,pad_max-padvar
clear_pad_1:
       clr  *r1+
       dect r2
       jne  clear_pad_1
       rt
*// clear_pad

*********************************************************************************
*
* Copy VDP copy to PAD
*
copy_vdp_copy_to_pad:
       li   r0,vdp_copy
       li   r1,vdp_copy_pad
       li   r2,vdp_copy_end-vdp_copy
copy_vdp_copy_to_pad_1:
       mov  *r0+,*r1+
       dect r2
       jne copy_vdp_copy_to_pad_1
       rt
*// copy_vdp_copy_to_pad

*********************************************************************************
*
* Set graphics mode
*
graphics_mode:
       .proc
       li   r1,vregs2
graphics_mode_1:
       mov  *r1+,r0
       jlt  graphics_mode_2
       bl   @vwtr                      ; Set register
       jmp  graphics_mode_1
*      Return
graphics_mode_2:
       .endproc
*      VDP registers
vregs2:
       byte >39,>1c                        ; Unlock F18A
       byte >39,>1c                        ; Unlock F18A
       byte >00,>00                        ; Graphics I mode
       byte >01,>e3                        ; 16K, display on, interrupts enabled
       byte >02,name_table/>400            ; Name table
       byte >03,tile_attr_table/>40        ; Tile attribute table
       byte >04,pattern_table/>800         ; Pattern table
       byte >05,sprite_attr_table/>80      ; Sprite attribute table
       byte >06,sprite_pattern_table/>800  ; Sprite pattern table
       byte >07,>01                        ; Backdrop color
       byte >0a,name_table_2/>400          ; Name table 2
       byte >0b,tile_attr_table/>40        ; Tile attribute table 2
       byte >1a,>00                        ; TL2 vertical scroll offset
       byte >1d,>44                        ; 1K bytes between sprite + tile planes
       byte >1f,>d0                        ; Bitmap enable, fat pixels
       byte >20,bitmap_dst/>40             ; Bitmap address
       byte >21,>00                        ; Bitmap x
       byte >22,bitmap_top                 ; Bitmap y
       byte >23,>00                        ; Bitmap width
       byte >24,bitmap_height              ; Bitmap height
       byte >31,>ea                        ; TL2, ROW30, ECM2 tiles + sprites, real y coord
       byte >32,>01                        ; TL2 not always on top
       byte >33,>03                        ; Stop sprite
       data -1
*// graphics_mode

*********************************************************************************
*
* Initialize VDP RAM
*
init_vdp_ram:
       .proc
*      Clear all
       clr  r0
       clr  r1
       li   r2,>4000
       bl   @vsmw
*      Bitmap
       li   r0,bitmap_src
       li   r1,bmp
       li   r2,>2000
       bl   @vmbw
       li   r0,bitmap_gap
       li   r1,>1100
       li   r2,64
       bl   @vsmw
*      Load palette
       li  r0,>2fc0                    ; Reg 47, DPM = 1, AUTO INC = 1
       li  r1,PAL0
       bl   @vwtr
       li  r2,128                      ; 128 bytes for 64 colors
load_palette_loop:
       movb *r1+,*r15                  ; Every two bytes written to the VDP now go to the palette registers
       dec  r2
       jne  load_palette_loop
       li   r0,>2f00                   ; Reg 47, DPM = 0, AUTO INC = 0
       bl   @vwtr
*      Name table
       li   r0,name_table
       li   r1,MD0
       li   r2,background_image_height_char*screen_width_char
       bl   @vmbw
*      Name table_2
       li   r0,name_table_2
       li   r1,MD0+(background_image_height_char*screen_width_char)
       li   r2,background_image_height_char*screen_width_char
       bl   @vmbw
       li   r0,name_table_2+(background_image_height_char*screen_width_char)
       li   r1,>0a00
       li   r2,(screen_height_char-background_image_height_char)*screen_width_char
       bl   @vsmw
*      Tile attributes
       li   r0,tile_attr_table
       li   r1,TAT0                    ; Use palette 1
       li   r2,n_char_patterns
       bl   @vmbw
*      Character patterns
       li   r0,pattern_table
       li   r1,P0_0
       li   r2,n_char_patterns*8
       bl   @vmbw
       li   r0,pattern_table_p1
       li   r1,P1_0
       li   r2,n_char_patterns*8
       bl   @vmbw
*      Sprite patterns
       li   r0,16*8+sprite_pattern_table
       li   r1,S0_16
       li   r2,n_sprite_patterns*32
       bl   @vmbw
       li   r0,16*8+sprite_pattern_table_p1
       li   r1,S1_16
       li   r2,n_sprite_patterns*32
       bl   @vmbw
*      Sprites off
       li   r0,sprite_attr_table
       li   r1,>f000
       bl   @vsbw
*      GPU program
       li   r0,gpu_program_tmp
       li   r1,gpu_prg_start
       li   r2,gpu_prg_end-gpu_prg_start
       bl   @vmbw
*      GPU data
       li   r0,gpu_data
       li   r1,gpu_data_start
       li   r2,gpu_data_end-gpu_data_start
       bl   @vmbw
*      Graphics data
       li   r0,graphics_data_1
       li   r1,gfx_data_1
       li   r2,gfx_data_1_end-gfx_data_1
       bl   @vmbw
       .endproc
*// init_vdp_ram

***************************************************************************
*
* Check key
*
* r0: Column in MSB: >0000 - >0700, Row in LSB: >0006 - >0014
*
*       Column   0      1    2    3    4    5     6       7
*     Row
*     >0006      =      .    ,    M    N    /    Fire    Fire
*     >0008    Space    L    K    J    H    ;    Left    Left
*     >000A    Enter    O    I    U    Y    P    Right   Right
*     >000C             9    8    7    6    0    Down    Down
*     >000E    Fctn     2    3    4    5    1    Up      Up
*     >0010    Shift    S    D    F    G    A
*     >0012    Ctrl     W    E    R    T    Q
*     >0014             X    C    V    B    Z
*
* On return NEQ means that the key is pressed
*
KEY_FI EQU  >0606
KEY_LF EQU  >0608
KEY_RG EQU  >060A
KEY_DN EQU  >060C
KEY_UP EQU  >060E
KEY_CM EQU  >0206
KEY_FS EQU  >0106
KEY_0  EQU  >050C
KEY_1  EQU  >050E
KEY_2  EQU  >010E
KEY_3  EQU  >020E
KEY_4  EQU  >030E
KEY_5  EQU  >040E
KEY_6  EQU  >040C
KEY_7  EQU  >030C
KEY_8  EQU  >020C
KEY_9  EQU  >010C
KEY_A  EQU  >0510
KEY_B  EQU  >0414
KEY_C  EQU  >0214
KEY_D  EQU  >0210
KEY_E  EQU  >0212
KEY_F  EQU  >0310
KEY_G  EQU  >0410
KEY_H  EQU  >0408
KEY_I  EQU  >020A
KEY_J  EQU  >0308
KEY_K  EQU  >0208
KEY_L  EQU  >0108
KEY_M  EQU  >0306
KEY_N  EQU  >0406
KEY_O  EQU  >010A
KEY_P  EQU  >050A
KEY_Q  EQU  >0512
KEY_R  EQU  >0312
KEY_S  EQU  >0110
KEY_T  EQU  >0412
KEY_U  EQU  >030A
KEY_V  EQU  >0314
KEY_W  EQU  >0112
KEY_X  EQU  >0114
KEY_Y  EQU  >040A
KEY_Z  EQU  >0514
KEY_SP EQU  >0008
KEY_EN EQU  >000A
KEY_FN EQU  >000E
KEY_SH EQU  >0010
KEY_CT EQU  >0012
JOY_FI EQU  >0606
JOY_LT EQU  >0608
JOY_RT EQU  >060A
JOY_DN EQU  >060C
JOY_UP EQU  >060E
*
check_key:
       li   r12,>0024                  ; CRU address of the column decoder
       ldcr r0,3                       ; Select column
       andi r0,>00ff
       mov  r0,r12                     ; Select row
       tb   0                          ; Test key, EQ if not pressed
       rt
*// check_key

***************************************************************************
*
* CPU memory copy
*
copy:
       mov  *r0+,*r1+
       dect r2
       jne  copy
       rt
*// copy

***************************************************************************
*
* Program includes
*
       copy "vdp.a99"
       copy "math.a99"
gpu_prg_start:
       copy "gpu.a99"
gpu_prg_end:
       equ  $

lower_memory_max:
       equ  $

***************************************************************************
*
* Data
*
       aorg >a000

init_data:
       data >8000                      ; x
       data >1000                      ; y
       data >0000                      ; dx
       data >0000                      ; dy
       data >0000                      ; vx
       data >0000                      ; vy
       data >8000                      ; angle
       data >0000                      ; anglular_velocity
       data >0000                      ; power
       data >0000                      ; reverse
       data max_power_road             ; max_power
       data max_reverse_road           ; max_reverse
init_data_end:
       equ  $

other_karts:
       data >6800                      ; x
       data >1000                      ; y
       data >6800                      ; x
       data >1a00                      ; y
       data >7200                      ; x
       data >1000                      ; y
       data >7200                      ; x
       data >1a00                      ; y
       data >7c00                      ; x
       data >1a00                      ; y
       data -1

***************************************************************************
*
* Data includes
*
       copy "data.a99"
gpu_data_start:
       copy "gpu-data.a99"
gpu_data_end:
       equ  $
bmp:
       bcopy "bitmap.bin"
dir_map:
       copy "dir-map.a99"

gfx_data_1:
       data >1800,>2000                ; width in pixels (2 pixels per fat pixel), height in pixels
       bcopy "sprites/barrel.bin"
       data >2800,>1000
       bcopy "sprites/kart.bin"
gfx_data_1_end:
       equ  $

barrel_sprite:                         ; 196 bytes
       equ  graphics_data_1
kart_sprite:                           ; 164 bytes
       equ  barrel_sprite+196

upper_memory_max:
       equ  $

       end
