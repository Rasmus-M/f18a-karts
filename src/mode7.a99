*********************************************************************
*
* Mode 7 demo
* For the TI-99/4A home computer
*
* January 2024 -
* Rasmus Moustgaard <rasmus.moustgaard@gmail.com>
*

       copy "macros.a99"

**
* Constants
*
debug:
       equ  0
screen_width_char:
       equ  32
screen_height_char:
       equ  30
screen_size_char:
       equ  screen_width_char*screen_height_char
screen_width:
       equ  screen_width_char*8
screen_height:
       equ  screen_height_char*8
background_image_top_char:
       equ  2
background_image_height_char:
       equ  4
bitmap_top:
       equ  (background_image_top_char+background_image_height_char)*8
bitmap_width:
       equ  128
bitmap_dst_height:
       equ  63
bitmap_gap_top:
       equ  bitmap_top+bitmap_dst_height
bitmap_gap_height:
       equ  1
bitmap_src_top:
       equ  bitmap_gap_top+bitmap_gap_height
bitmap_src_height:
       equ  128
bitmap_height:
       equ  bitmap_dst_height+bitmap_gap_height+bitmap_src_height
n_char_patterns:
       equ  10
n_sprite_patterns:
       equ  2
max_speed:
       equ  64

**
* Memory mapped addresses
*
vdpwd:                                 ; VDP write data
       equ  >8c00
vdpwa:                                 ; VDP set read/write address
       equ  >8c02
vdprd:                                 ; VDP read data
       equ  >8800
vdpsta:                                ; VDP status
       equ  >8802
sound:                                 ; Sound
       equ  >8400
spchwt:
       equ  >9400
spchrd:
       equ  >9000


**
* VDP memory map
*
gpu_program_tmp:
       equ  >0000
bitmap_base:                           ; 128 x 192 x 4 bbp = 12K
       equ  >0000
bitmap_dst:
       equ  bitmap_base
bitmap_gap:
       equ  bitmap_base + (bitmap_dst_height * bitmap_width / 2)
bitmap_src:
       equ  bitmap_gap + (bitmap_gap_height * bitmap_width / 2)
name_table:                            ; Name table (note: we only need 6 rows of this)
       equ  bitmap_src + (bitmap_src_height * bitmap_width / 2) ; = >3000
gpu_data:                              ; Data shared between CPU and GPU (64 bytes)
       equ  >30C0
tile_attr_table:                       ; Tile attribute table
       equ  >3100
sprite_attr_table:                     ; Sprite attribute table = >3000
       equ  >3180
graphics_data_1:                       ; >600 bytes for scaled sprites
       equ  >3200
pattern_table:                         ; Pattern table
       equ  >3800
sprite_pattern_table:                  ; Sprite pattern table
       equ  >3800
graphics_data_2:                       ; >300 bytes for scaled sprites
       equ  >3900
pattern_table_p1:                      ; Pattern table plane 1
       equ  >3c00
sprite_pattern_table_p1:               ; Sprite pattern table plane 1
       equ  >3c00
graphics_data_3:                       ; >300 bytes for scaled sprites
       equ  >3d00
gpu_program:                           ; Approx. >600 bytes for scaled sprites
       equ  >4000

**
* Scratch pad
*
pad:
       equ  >8300
wrksp:
       equ  pad                        ; Workspace
r0lb:
       equ  wrksp+1
r1lb:
       equ  wrksp+3
r2lb:
       equ  wrksp+5
r3lb:
       equ  wrksp+7
wrksp2:
       equ  wrksp+>20
stack:
       equ  wrksp2+>20
**
* PAD constants
*
padcon:
       equ  stack+>16                  ; Start of pad constants
one:
       equ  padcon
**
* PAD variables
*
padvar:                                ; Start of pad variables
       equ  one+2
frame:
       equ padvar
x:                                     ; World x position (FP 8.8, unsigned, 0 <= x < 256)
       equ  frame+2
y:                                     ; World y position (FP 8.8, unsigned, 0 <= x < 128)
       equ  x+2
dx:                                    ; Direction vector x (FP 8.8, signed, -1 <= dx <= 1)
       equ  y+2
dy:                                    ; Direction vector y (FP 8.8, signed, -1 <= dy <= 1)
       equ  dx+2
vx:                                    ; Velocity vector x (FP 8.8, signed)
       equ  dy+2
vy:                                    ; Velocity vector y (FP 8.8, signed)
       equ  vx+2
angle:                                 ; View angle, 0 - 255, higher values rotates clockwise
       equ  vy+2
speed:                                 ; Speed, -max_speed - max_speed
       equ  angle+2
vdp_copy_pad:
       equ  speed+2
pad_max:
       equ  vdp_copy_pad+22

********************************************************************************
*
* Main program
*
       aorg >2000
main:
       limi 0                          ; Interrupts off
       lwpi wrksp                      ; Setup workspace
       li   r10,stack                  ; Setup stack pointer
       li   r15,vdpwd
       bl   @one_time_init
*      Run GPU program, which moves it into high GPU RAM
       bl   @run_gpu
*      Init
       li   r0,init_data
       li   r1,x
       li   r2,16
       bl   @copy
       bl   @calculate_dx_dy
       bl   @upload_gpu_data
loop:
       bl   @wait_gpu
       bl   @vsync
       bl   @vsync
       bl   @trigger_gpu
       bl   @display_sprites
       bl   @scroll_background
       bl   @user_input
       bl   @calculate_dx_dy
       bl   @move
       bl   @upload_gpu_data
       .ifeq debug,1
*      Upload bitmap again
       li   r0,bitmap_src
       bl   @vwad
       li   r1,bmp
       li   r2,>2000/8
       bl   @vdp_copy_pad
       .endif
       jmp  loop

*********************************************************************
*
* Wait for vertical retrace (CRU)
*
vsync:
       .proc
       movb @vdpsta,r12
vsync_1:
       clr  r12
       tb   2                          ; Test CRU bit for VDP interrupt
       jeq  vsync_1
       movb @vdpsta,r12
       .endproc
*// vsync

*********************************************************************************
*
* Run GPU program
*
run_gpu:
       .proc
       li   r0,gpu_program_tmp/256+>3600
       bl   @vwtr
       li   r0,gpu_program_tmp%256+>3700
       bl   @vwtr
       .endproc
*// run_gpu

*********************************************************************************
*
* Wait for the GPU to be idle
*
wait_gpu:
       .proc
       li   r0,>0f02
       bl   @vwtr
wait_gpu_1:
       movb @vdpsta,r0
       jlt  wait_gpu_1
       li   r0,>0f00
       bl   @vwtr
       .endproc
*// wait_gpu

*********************************************************************************
*
* Resume GPU after idle
*
trigger_gpu:
       .proc
       li   r0,>3801
       bl   @vwtr
       .endproc
*// wait_gpu

*********************************************************************************
*
* Display sprites
*
display_sprites:
       .proc
       li   r0,sprite_attr_table
       bl   @vwad
*      Kart left sprite
       li   r1,>4e60
       mov  r1,*r15
       swpb r1
       mov  r1,*r15
       li   r1,>1404
       mov  r1,*r15
       swpb r1
       mov  r1,*r15
*      Kart right sprite
       li   r1,>4e80
       mov  r1,*r15
       swpb r1
       mov  r1,*r15
       li   r1,>1444
       mov  r1,*r15
       swpb r1
       mov  r1,*r15
*      Marker on source map
       mov  @y,r1
       andi r1,>7f00
       ai   r1,->1000
       ai   r1,bitmap_src_top*256
       movb r1,*r15
       mov  @x,r1
       ai   r1,->1000
       movb r1,*r15
       li   r1,>1000
       movb r1,*r15
       swpb r1
       movb r1,*r15
       .endproc
*// display_sprites

*********************************************************************************
*
* Scroll background
*
scroll_background:
       .proc
       mov  @angle,r0
       sla  r0,2
       andi r0,>00ff
       ori  r0,>1b00
       bl   @vwtr
       .endproc
*// scroll_background

*********************************************************************************
*
* User input
*
user_input:
       .proc
       clr  r1
       li   r0,JOY_FI
       bl   @check_key
       jeq  user_input_1
       inc  r1
user_input_1:
*      Left
       tb   1
       jeq  user_input_2
       dec  @angle
       mov  @speed,r0
       ci   r0,max_speed/2
       jlt  user_input_3
       dec  @angle
       jmp  user_input_3
user_input_2:
*      Right
       tb   2
       jeq  user_input_3
       inc @angle
       mov  @speed,r0
       ci   r0,max_speed/2
       jlt  user_input_3
       inc  @angle
user_input_3:
*      Backwards
       abs  r1
       jne  user_input_4
       tb   3
       jeq  user_input_6
user_input_4:
       mov  @speed,r0
       ci   r0,-(max_speed-1)
       jlt  user_input_5
       dec  r0
user_input_5:
       mov  r0,@speed
       jmp  user_input_8
user_input_6:
*      Forwards
       tb   4
       jeq  user_input_8
       mov  @speed,r0
       ci   r0,max_speed-1
       jgt  user_input_7
       inc  r0
user_input_7:
       mov  r0,@speed
user_input_8:
       .endproc
*// user_input

*********************************************************************************
*
* Calculate directional vector
*
calculate_dx_dy:
       .proc
*      dx
       mov  @angle,r1
       bl   @cos
       mov  r1,@dx
*      dy
       mov  @angle,r1
       bl   @sin
       mov  r1,@dy
       .endproc
*// calculate_dx_dy

*********************************************************************************
*
* Move
*
move:
       .proc
*      x
       mov  @dx,r1
       mov  @speed,r2
       sla  r2,2
       bl   @mpy_fp88
       mov  r1,@vx                     ; vx = dx * speed
       a    @vx,@x                     ; x += vx
*      y
       mov  @dy,r1
       mov  @speed,r2
       sla  r2,2
       bl   @mpy_fp88
       mov  r1,@vy                     ; vy = dy * speed
       a    @vy,@y                     ; y += vy
       .endproc
*// move

*********************************************************************************
*
* Upload GPU data
*
upload_gpu_data:
       .proc
*      x
       li   r0,gpu_x
       mov  @x,r1
       bl   @vsww
*      y
       li   r0,gpu_y
       mov  @y,r1
       bl   @vsww
*      dx
       li   r0,gpu_dx
       mov  @dx,r1
       bl   @vsww
*      dy
       li   r0,gpu_dy
       mov  @dy,r1
       bl   @vsww
       .endproc
*// upload_gpu_data

*********************************************************************************
*
* One time initializations
*
one_time_init:
       .proc
       li   r0,1
       mov  r0,@one
       bl   @clear_pad
       bl   @copy_vdp_copy_to_pad
       bl   @graphics_mode
       bl   @init_vdp_ram
       .endproc
*// one_time_init

*********************************************************************************
*
* Clear PAD
*
clear_pad:
       li   r1,padvar
       li   r2,pad_max-padvar
clear_pad_1:
       clr  *r1+
       dect r2
       jne  clear_pad_1
       rt
*// clear_pad

*********************************************************************************
*
* Copy VDP copy to PAD
*
copy_vdp_copy_to_pad:
       li   r0,vdp_copy
       li   r1,vdp_copy_pad
       li   r2,vdp_copy_end-vdp_copy
copy_vdp_copy_to_pad_1:
       mov  *r0+,*r1+
       dect r2
       jne copy_vdp_copy_to_pad_1
       rt
*// copy_vdp_copy_to_pad

*********************************************************************************
*
* Set graphics mode
*
graphics_mode:
       .proc
       li   r1,vregs2
graphics_mode_1:
       mov  *r1+,r0
       jlt  graphics_mode_2
       bl   @vwtr                      ; Set register
       jmp  graphics_mode_1
*      Return
graphics_mode_2:
       .endproc
*      VDP registers
vregs2:
       byte >39,>1c                        ; Unlock F18A
       byte >39,>1c                        ; Unlock F18A
       byte >00,>00                        ; Graphics I mode
       byte >01,>e3                        ; 16K, display on, interrupts enabled
       byte >02,name_table/>400            ; Name table
       byte >03,tile_attr_table/>40        ; Tile attribute table
       byte >04,pattern_table/>800         ; Pattern table
       byte >05,sprite_attr_table/>80      ; Sprite attribute table
       byte >06,sprite_pattern_table/>800  ; Sprite pattern table
       byte >07,>01                        ; Backdrop color
       byte >1d,>44                        ; 1K bytes between sprite + tile planes
       byte >1f,>d0                        ; Bitmap enable, fat pixels
       byte >20,bitmap_dst/>40             ; Bitmap address
       byte >21,>00                        ; Bitmap x
       byte >22,bitmap_top                 ; Bitmap y
       byte >23,>00                        ; Bitmap width
       byte >24,bitmap_height              ; Bitmap height
       byte >31,>6a                        ; ROW30, ECM2 tiles + sprites, real y coord
       byte >33,>03                        ; Stop sprite
       data -1
*// graphics_mode

*********************************************************************************
*
* Initialize VDP RAM
*
init_vdp_ram:
       .proc
*      Clear all
       clr  r0
       clr  r1
       li   r2,>4000
       bl   @vsmw
*      Bitmap
       li   r0,bitmap_src
       li   r1,bmp
       li   r2,>2000
       bl   @vmbw
       li   r0,bitmap_gap
       li   r1,>1100
       li   r2,64
       bl   @vsmw
*      Load palette
       li  r0,>2fc0                    ; Reg 47, DPM = 1, AUTO INC = 1
       li  r1,PAL0
       bl   @vwtr
       li  r2,128                      ; 128 bytes for 64 colors
load_palette_loop:
       movb *r1+,*r15                  ; Every two bytes written to the VDP now go to the palette registers
       dec  r2
       jne  load_palette_loop
       li   r0,>2f00                   ; Reg 47, DPM = 0, AUTO INC = 0
       bl   @vwtr
*      Name table
       li   r0,name_table+(background_image_top_char*screen_width_char)
       li   r1,MD0
       li   r2,background_image_height_char*screen_width_char
       bl   @vmbw
*      Tile attributes
       li   r0,tile_attr_table
       li   r1,TAT0                    ; Use palette 1
       li   r2,n_char_patterns
       bl   @vmbw
*      Character patterns
       li   r0,pattern_table
       li   r1,P0_0
       li   r2,n_char_patterns*8
       bl   @vmbw
       li   r0,pattern_table_p1
       li   r1,P1_0
       li   r2,n_char_patterns*8
       bl   @vmbw
*      Sprite patterns
       li   r0,16*8+sprite_pattern_table
       li   r1,S0_16
       li   r2,n_sprite_patterns*32
       bl   @vmbw
       li   r0,16*8+sprite_pattern_table_p1
       li   r1,S1_16
       li   r2,n_sprite_patterns*32
       bl   @vmbw
*      Sprites off
       li   r0,sprite_attr_table
       li   r1,>f000
       bl   @vsbw
*      GPU program
       li   r0,gpu_program_tmp
       li   r1,gpu_prg_start
       li   r2,gpu_prg_end-gpu_prg_start
       bl   @vmbw
*      GPU data
       li   r0,gpu_data
       li   r1,gpu_data_start
       li   r2,gpu_data_end-gpu_data_start
       bl   @vmbw
*      Graphics data
       li   r0,graphics_data_1
       li   r1,gfx_data_1
       li   r2,gfx_data_1_end-gfx_data_1
       bl   @vmbw
       .endproc
*// init_vdp_ram

***************************************************************************
*
* Check key
*
* r0: Column in MSB: >0000 - >0700, Row in LSB: >0006 - >0014
*
*       Column   0      1    2    3    4    5     6       7
*     Row
*     >0006      =      .    ,    M    N    /    Fire    Fire
*     >0008    Space    L    K    J    H    ;    Left    Left
*     >000A    Enter    O    I    U    Y    P    Right   Right
*     >000C             9    8    7    6    0    Down    Down
*     >000E    Fctn     2    3    4    5    1    Up      Up
*     >0010    Shift    S    D    F    G    A
*     >0012    Ctrl     W    E    R    T    Q
*     >0014             X    C    V    B    Z
*
* On return NEQ means that the key is pressed
*
KEY_FI EQU  >0606
KEY_LF EQU  >0608
KEY_RG EQU  >060A
KEY_DN EQU  >060C
KEY_UP EQU  >060E
KEY_CM EQU  >0206
KEY_FS EQU  >0106
KEY_0  EQU  >050C
KEY_1  EQU  >050E
KEY_2  EQU  >010E
KEY_3  EQU  >020E
KEY_4  EQU  >030E
KEY_5  EQU  >040E
KEY_6  EQU  >040C
KEY_7  EQU  >030C
KEY_8  EQU  >020C
KEY_9  EQU  >010C
KEY_A  EQU  >0510
KEY_B  EQU  >0414
KEY_C  EQU  >0214
KEY_D  EQU  >0210
KEY_E  EQU  >0212
KEY_F  EQU  >0310
KEY_G  EQU  >0410
KEY_H  EQU  >0408
KEY_I  EQU  >020A
KEY_J  EQU  >0308
KEY_K  EQU  >0208
KEY_L  EQU  >0108
KEY_M  EQU  >0306
KEY_N  EQU  >0406
KEY_O  EQU  >010A
KEY_P  EQU  >050A
KEY_Q  EQU  >0512
KEY_R  EQU  >0312
KEY_S  EQU  >0110
KEY_T  EQU  >0412
KEY_U  EQU  >030A
KEY_V  EQU  >0314
KEY_W  EQU  >0112
KEY_X  EQU  >0114
KEY_Y  EQU  >040A
KEY_Z  EQU  >0514
KEY_SP EQU  >0008
KEY_EN EQU  >000A
KEY_FN EQU  >000E
KEY_SH EQU  >0010
KEY_CT EQU  >0012
JOY_FI EQU  >0606
JOY_LT EQU  >0608
JOY_RT EQU  >060A
JOY_DN EQU  >060C
JOY_UP EQU  >060E
*
check_key:
       li   r12,>0024                  ; CRU address of the column decoder
       ldcr r0,3                       ; Select column
       andi r0,>00ff
       mov  r0,r12                     ; Select row
       tb   0                          ; Test key, EQ if not pressed
       rt
*// check_key

***************************************************************************
*
* CPU memory copy
*
copy:
       mov  *r0+,*r1+
       dect r2
       jne  copy
       rt
*// copy

***************************************************************************
*
* Program includes
*
       copy "vdp.a99"
       copy "math.a99"
gpu_prg_start:
       copy "gpu.a99"
gpu_prg_end:
       equ  $

lower_memory_max:
       equ  $

***************************************************************************
*
* Data
*
       aorg >a000

init_data:
       data >8800                      ; x
       data >1400                      ; y
       data >0000                      ; vx
       data >0000                      ; vy
       data >0100                      ; dx
       data >0000                      ; dy
       data 0                          ; angle
       data 0                          ; speed

***************************************************************************
*
* Data includes
*
       copy "data.a99"
gpu_data_start:
       copy "gpu-data.a99"
gpu_data_end:
       equ  $
bmp:
       .ifeq debug,1
       bcopy "bitmap-debug.bin"
       .else
       bcopy "bitmap.bin"
       .endif

gfx_data_1:
       data >1800,>2000                ; width in pixels (2 pixels per fat pixel), height in pixels
       bcopy "barrel.bin"
gfx_data_1_end:
       equ  $
barrel_sprite:                         ; 196 bytes
       equ  graphics_data_1

upper_memory_max:
       equ  $

       end
